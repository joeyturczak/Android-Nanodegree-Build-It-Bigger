apply plugin: 'com.android.application'

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "com.udacity.gradle.builditbigger"
        minSdkVersion 10
        targetSdkVersion 22
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }

    productFlavors {
        free {
            applicationId "com.udacity.gradle.builditbigger.free"
        }
        paid {
            applicationId "com.udacity.gradle.builditbigger.paid"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.11'
    // Added for AdMob
    compile project(':javajokes')
    compile project(':jokeactivity')
    freeCompile 'com.google.android.gms:play-services:8.1.0'
    compile project(path: ':backend', configuration: 'android-endpoints')
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile project(path: ':backend', configuration: 'android-endpoints')
}

// Starts Google Cloud Endpoints Module
task startGCE {
    dependsOn tasks.getByPath(":backend:appengineRun")
}

// Runs tests
task runTests {
    dependsOn 'check'
    dependsOn 'connectedCheck'
    mustRunAfter 'startGCE'
}

// Stops the Google Cloud Endpoints Module
task stopGCE {
    dependsOn tasks.getByPath(":backend:appengineStop")
    mustRunAfter 'runTests'
}

// Runs the Google Cloud Endpoints Module and tests
task runGCEandTest {
    evaluationDependsOn ":backend"
    project(":backend") {
        appengine {
            daemon true
        }
    }
    dependsOn 'startGCE'
    dependsOn 'runTests'
    finalizedBy 'stopGCE'
}